// The toolkit's 2D pipeline shader — the union of the two wgpu-era dialects:
// the engine shader's wavy-blob effect (clip_circle.x == -999 sentinel) and the
// designer shader's window-corner rounding + circle clip + blur-behind branch
// (negative alpha samples the backdrop). Clients that don't use a feature pay
// nothing: radius 0 disables corner rounding, the backdrop is renderer-managed,
// and plain quads take the final `return in.color` path.

@group(0) @binding(0) var t_backdrop: texture_2d<f32>;
@group(0) @binding(1) var s_backdrop: sampler;

struct WindowInfo {
    window_size: vec2<f32>,
    corner_radius: f32,
    padding: f32,
}

@group(0) @binding(2) var<uniform> window_info: WindowInfo;

fn is_outside_window_corners(pos: vec2<f32>) -> bool {
    let w = window_info.window_size.x;
    let h = window_info.window_size.y;
    let r = window_info.corner_radius;

    if (r <= 0.0) {
        return false;
    }
    // Top-left
    if (pos.x < r && pos.y < r) {
        let dx = pos.x - r;
        let dy = pos.y - r;
        return (dx * dx + dy * dy) > r * r;
    }
    // Top-right
    if (pos.x > w - r && pos.y < r) {
        let dx = pos.x - (w - r);
        let dy = pos.y - r;
        return (dx * dx + dy * dy) > r * r;
    }
    // Bottom-left
    if (pos.x < r && pos.y > h - r) {
        let dx = pos.x - r;
        let dy = pos.y - (h - r);
        return (dx * dx + dy * dy) > r * r;
    }
    // Bottom-right
    if (pos.x > w - r && pos.y > h - r) {
        let dx = pos.x - (w - r);
        let dy = pos.y - (h - r);
        return (dx * dx + dy * dy) > r * r;
    }
    // Boundary check
    if (pos.x < 0.0 || pos.x > w || pos.y < 0.0 || pos.y > h) {
        return true;
    }
    return false;
}

// Per-batch rounded-rect clip: rect0 = [cx, cy, bx, by] (center + SDF half-extents),
// rect1 = [corner radius, enabled flag, 0, 0]. Physical pixels, like clip_position.
struct RRectClip {
    rect0: vec4f,
    rect1: vec4f,
}
var<push_constant> rrect_clip: RRectClip;

struct VertexOutput {
    @builtin(position) clip_position: vec4f,
    @location(0) color: vec4f,
    @location(1) ndc_position: vec2f,
    @location(2) clip_circle: vec3f,
}

@vertex
fn vs_main(
    @location(0) position: vec2f,
    @location(1) color: vec4f,
    @location(2) clip_circle: vec3f,
) -> VertexOutput {
    var out: VertexOutput;
    out.clip_position = vec4f(position, 0.0, 1.0);
    out.color = color;
    out.ndc_position = position;
    out.clip_circle = clip_circle;
    return out;
}

@fragment
fn fs_main(in: VertexOutput) -> @location(0) vec4f {
    // Wavy-blob effect (engine shader.wgsl): the -999 sentinel renders a
    // rippled, fading disc in NDC space.
    if (in.clip_circle.x == -999.0) {
        let y = length(vec2f(in.ndc_position.x, in.ndc_position.y));
        let x = atan2(in.ndc_position.y, in.ndc_position.x);

        // Wavy boundary radius with 7 lobes
        let R_theta = 0.60 + 0.06 * sin(7.0 * x);

        // Radial density: 1.0 at center, fading out to 0.0 at R_theta
        let density = 1.0 - smoothstep(R_theta - 0.25, R_theta, y);

        // Sine wave effect driven by the x value (distance around the circle)
        let sin_effect = sin(7.0 * x);

        // Normalized radius from 0.0 (center) to 1.0 (boundary)
        let r_normalized = clamp(y / R_theta, 0.0, 1.0);

        let gray = in.color.xyz;

        // Scale the ripple amplitude by the normalized radius to fade it out at the center
        let alpha = clamp(density * (1.0 - r_normalized * 0.25 * (1.0 - sin_effect)), 0.0, 1.0);

        if (y > R_theta + 0.02) {
            discard;
        }

        let final_alpha = alpha * (1.0 - smoothstep(R_theta - 0.02, R_theta + 0.02, y)) * in.color.w;
        return vec4f(gray, final_alpha);
    }

    if (is_outside_window_corners(in.clip_position.xy)) {
        discard;
    }
    if (in.clip_circle.z > 0.0) {
        let dx = in.clip_position.x - in.clip_circle.x;
        let dy = in.clip_position.y - in.clip_circle.y;
        if (dx * dx + dy * dy > in.clip_circle.z * in.clip_circle.z) {
            discard;
        }
    }
    // Rounded-rect clip (per-batch): SDF of the round-cornered box; outside discards.
    if (rrect_clip.rect1.y > 0.5) {
        let q = abs(in.clip_position.xy - rrect_clip.rect0.xy) - rrect_clip.rect0.zw;
        let d = length(max(q, vec2f(0.0))) - rrect_clip.rect1.x;
        if (d > 0.0) {
            discard;
        }
    }

    // Blur-behind plate: negative alpha mixes the (blurred) backdrop with the
    // plate color at |alpha| opacity.
    if (in.color.a < 0.0) {
        let tex_size = vec2f(textureDimensions(t_backdrop));
        let clean_backdrop = textureSample(t_backdrop, s_backdrop, in.clip_position.xy / tex_size);

        var blurred = vec4f(0.0);
        var total_weight = 0.0;

        // 7x7 Gaussian blur kernel
        for (var x = -3.0; x <= 3.0; x += 1.0) {
            for (var y = -3.0; y <= 3.0; y += 1.0) {
                let offset = vec2f(x, y) * 2.0; // sample every 2 pixels for a wider blur
                let sample_uv = (in.clip_position.xy + offset) / tex_size;
                let weight = exp(-(x*x + y*y) / (2.0 * 2.0 * 2.0));
                blurred += textureSample(t_backdrop, s_backdrop, sample_uv) * weight;
                total_weight += weight;
            }
        }

        let backdrop_color = blurred / total_weight;
        let opacity = -in.color.a;
        let plate_color = vec4f(in.color.rgb, 1.0);
        let blurred_plate = mix(backdrop_color, plate_color, opacity);
        return mix(clean_backdrop, blurred_plate, opacity);
    }

    return in.color;
}
